---
title: Investigating Two Fp Groups of A. Cavicchioli 
layout: default
toc: Documentation
subtoc: Examples
---

<hr />

In research on some fundamental groups, A. Cavicchioli (Modena)
considered a family of groups defined by the presentations<br />

<p class="center">
G = &lang; a, b | a b a<sup>-2</sup> b a b<sup>-1</sup> =
a ( b<sup>-1</sup> a<sup>3</sup> b<sup>-1</sup> a<sup>-3</sup> ) <sup>q</sup>
= 1 &rang;. </p>
<br />

We investigate the cases q = 1 and q = 2. These examples were first
described in pre-GAP terms in a
<a href="/Doc/Talks/talks.html#cavicchioli">paper</a>
'An elementary introduction to computational methods for finitely
presented groups', a GAP&nbsp;3 code was given in a
<a href="/Doc/Talks/talks.html#rennes">talk</a> by
{% include namelink.html name="Alexander Hulpke" %}.

<p />
The following is an updated GAP&nbsp;4 version.
The input used is available in form of a plain
GAP&nbsp;4 <a href="cavicchioli.in">input&nbsp;file</a>.

<hr />

<h3>
We first investigate the group h with the parameter q=1.
</h3>

We define the fp group h and its subgroup s generated by the first
generator.

<pre class="gapexample">
gap> f := FreeGroup(2); a:=f.1;; b:=f.2;;
&lt;free group on the generators [ f1, f2 ]>
gap> h := f/[a*b*a^-2*b*a*b^-1,(b^-1*a^3*b^-1*a^-3)^1*a];
&lt;fp group on the generators [ f1, f2 ]>
gap> s := Subgroup(h,[h.1]);
Group([ f1 ])
</pre>

In order to see the working of the Todd-Coxeter method determining the
index of s in h we set a suitable print level.

<pre class="gapexample">
gap> SetInfoLevel(InfoFpGroup,3);
gap> Index(h,s);
#I  CosetTableFromGensAndRels called:
#I      defined deleted alive   maximal
#I  	  13	  1	  12	  13
12
</pre>

We determine the permutation representation of h on the cosets of s and
the order of that permutation group.

<pre class="gapexample">
gap> hom := FactorCosetAction(h,s);
[ f1, f2 ] -> [ (2,4,3,7,5)(6,8,10,11,9), (1,2,6,8,3)(5,9,12,10,7) ]
gap> p := Image(hom);
Group([ (2,4,3,7,5)(6,8,10,11,9), (1,2,6,8,3)(5,9,12,10,7) ])
gap> Size(p);
60
</pre>

Encouraged by the success we now look at the regular permutation
representation of h.

<pre class="gapexample">
gap> Index(h,TrivialSubgroup(h));
#I  CosetTableFromGensAndRels called:
#I      defined deleted alive   maximal
#I  	  133	  13	  120	  133
120
</pre>

Next we want to learn more about the structure of the group s the order of
which we know to be 120.

<pre class="gapexample">
gap> AbelianInvariants(h);
[  ]
</pre>

So the group h is perfect and, in fact, isomorphic to SL(2,5).

<h3>
We next try the same procedure with the group g with the parameter q=2.
</h3>

<pre class="gapexample">
gap> g := f/[a*b*a^-2*b*a*b^-1,(b^-1*a^3*b^-1*a^-3)^2*a];
&lt;fp group on the generators [ f1, f2 ]>
gap> Index(g,Subgroup(g,[g.1]));
</pre>

In the following output describing the performance of the Todd-Coxeter
method, which in this case does not succeed within the maximally allowed
256000 coset numbers, we leave out several lines in the printing below.

<pre>
#I  CosetTableFromGensAndRels called:
#I      defined deleted alive   maximal
#I  	  999	  1	  998	  999
#I  	  1997	  3	  1994	  1996
               [ ... some lines deleted ... ]
#I  	  254815  185	  254630  254693
#I  	  255812  188	  255624  255690
Error, the coset enumeration has defined more than 256000 cosets
called from
TCENUM.CosetTableFromGensAndRels( fgens, grels, fsgens ) called from
CosetTableFromGensAndRels( fgens, grels, fsgens ) called from
TryCosetTableInWholeGroup( H ) called from
CosetTableInWholeGroup( H ) called from
IndexInWholeGroup( H ) called from
...
Entering break read-eval-print loop ...
type 'return;' if you want to continue with a new limit of 512000 cosets,
type 'quit;' if you want to quit the coset enumeration,
type 'maxlimit := 0; return;' in order to continue without a limit
brk> quit;
</pre>

Rather than continuing the enumeration with an increased limit we have
quitted it in order to try a different approach. We can now reset the print
level to reduce the output.

<pre class="gapexample">
gap> SetInfoLevel(InfoFpGroup,0);
</pre>

We first check that the group g is perfect.

<pre class="gapexample">
gap> AbelianInvariants(g);
[  ]
</pre>

Next we determine all subgroups of g of index less than or equal to 15
and identify the type of the transitive permutation groups acting on their
cosets.

<pre class="gapexample">
gap> u := LowIndexSubgroupsFpGroup(g,TrivialSubgroup(g),15);;
gap> u := Filtered(u,i->Index(g,i)>1);;
gap> list := List(u,i->Image(FactorCosetAction(g,i)));;
gap> Length(list);
24
gap> id := List([1..24],
> i->[Index(g,u[i]),Size(list[i]),TransitiveIdentification(list[i])]);;
gap> Collected(id);
[ [ [ 11, 660, 5 ], 2 ], [ [ 11, 19958400, 7 ], 8 ], [ [ 12, 660, 179 ], 1 ], 
  [ [ 12, 95040, 295 ], 4 ], [ [ 12, 239500800, 300 ], 3 ], 
  [ [ 13, 3113510400, 8 ], 6 ] ]
</pre>

We ensure that all these permutation groups are simple.

<pre class="gapexample">
gap> Collected(List(list,i->IsSimple(i)));
[ [ true, 24 ] ]
</pre>

From the orders we therefore see that the occurring groups are isomorphic
to the alternating groups of degrees 11, 12, and 13, the group PSL(2,11),
and the Mathieu group M<sub>12</sub>.
<br />
We now obtain the subdirect product of the permutation groups above as
the factor group of the intersection of the cores of the permutation
representations.

<pre class="gapexample">
gap> cores := List(u,i->Core(g,i));;
gap> kernel := Intersection(cores);
Group(&lt;fp, no generators known>)
gap> hom := NaturalHomomorphismByNormalSubgroup(g,kernel);;
gap> image := Image(hom);;
</pre>

We determine the degree of the subdirect product:

<pre class="gapexample">
gap> IsPermGroup(image);
true
gap> NrMovedPoints(image);
237
</pre>

The order of the subdirect product is huge.

<pre class="gapexample">
gap> Size(image);
187838840781137083803280175646550961040094223793955611966199644315614527534543\
5097975617726355751505731109984424427520000000000000000000000000000000000000
</pre>

We hence suspect that the group g may be infinite. It is perfect,
but there is a chance that some of its subgroups may be not. So we determine
the types of the commutator factor groups of the subgroups we had found
before.

<pre class="gapexample">
gap> List(u,i->AbelianInvariants(i));
[ [ 11, 13 ], [ 2, 3, 9 ], [ 11, 13 ], [ 11, 13 ], [ 11, 13 ], [ 0, 0, 2 ], 
  [ 2, 2, 8 ], [ 2, 3, 9 ], [ 11, 13 ], [ 2, 2, 8 ], [ 11, 13 ], [ 2, 2, 8 ], 
  [ 2, 2, 8 ], [ 2, 2, 8 ], [ 2, 2, 3 ], [ 2, 2, 3 ], [ 2, 2, 3 ], 
  [ 2, 2, 3 ], [ 2, 2, 3 ], [ 2, 2, 3 ], [ 2, 2, 3 ], [ 2, 2, 3 ], 
  [ 2, 2, 3 ], [ 2, 2, 3 ] ]
</pre>

The sixth of these types is [ 0, 0, 2 ], telling us that indeed the
corresponding subgroup has an abelian factor group that is a direct product
of a free abelian group of rank 2 times a cyclic group of order 2.
